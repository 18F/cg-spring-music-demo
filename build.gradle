apply plugin: 'java'

apply plugin: 'eclipse'
apply plugin: 'idea'

apply plugin: 'tomcat'
apply plugin: 'war'

sourceCompatibility = 1.6
version = '1.0'

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/main/java'
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Spring Sample Application', 'Implementation-Version': version
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:0.9.8'
    }
}

repositories {
    mavenCentral()
    mavenRepo url: "http://maven.springframework.org/milestone"
}

dependencies {
    // Spring and dependencies
    compile "org.springframework:spring-context:${springVersion}"
    compile "org.springframework:spring-webmvc:${springVersion}"
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.8'

    // Spring Data
    compile 'org.springframework.data:spring-data-jpa:1.3.2.RELEASE'
    compile 'org.springframework.data:spring-data-redis:1.0.4.RELEASE'
    compile 'org.springframework.data:spring-data-mongodb:1.2.1.RELEASE'

    // JPA Persistence
    compile "commons-dbcp:commons-dbcp:1.2.2"
    compile 'org.hibernate:hibernate-entitymanager:4.1.9.Final'
    runtime 'com.h2database:h2:1.3.162'
    runtime 'mysql:mysql-connector-java:5.1.25'
    runtime 'postgresql:postgresql:9.1-901-1.jdbc4'

    // Cloud Foundry runtime
    compile 'org.cloudfoundry:cloudfoundry-runtime:0.8.4'

    // JSR-303 validation
    compile 'javax.validation:validation-api:1.0.0.GA'
    compile 'org.hibernate:hibernate-validator:4.3.0.Final'

    // Logging
    compile 'org.slf4j:slf4j-api:1.7.2'
    compile 'org.slf4j:slf4j-log4j12:1.7.2'

    // New Relic
    compile 'com.newrelic.agent.java:newrelic-agent:2.19.1'

    // Servlet
    compile "javax.servlet:jstl:1.2"
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'

    // Testing
    testCompile 'junit:junit:4.8.2'

    // Spring Testing
    testCompile "org.springframework:spring-test:${springVersion}"

    // Tomcat
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}

war {
    // omit the version from the war file name
    version = ""
}

tomcatRun {
    outputFile = file('tomcat.log')
}
